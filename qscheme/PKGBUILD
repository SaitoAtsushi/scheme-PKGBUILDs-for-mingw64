_realname=qscheme
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.5.1
pkgrel=1
pkgdesc="A fast and small implementation of Scheme written in C."
arch=('any')
url="http://www.sof.ch/dan/qscheme/index-e.html"
license=('GPL')
depends=()
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "coreutils"
             "make"
             "${MINGW_PACKAGE_PREFIX}-gmp"
             "${MINGW_PACKAGE_PREFIX}-ffcall"
             "${MINGW_PACKAGE_PREFIX}-dlfcn")
source=("http://www.sof.ch/dan/qscheme/files/qscheme-0.5.1.tar.gz"
        "qscheme-mingw.patch")
sha256sums=('6d9f9574965af21b3c63c7b46caefcf4e06aa6fbbb02e5e5faf42f54851f1752'
            'SKIP')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/qscheme-mingw.patch
}

build() {
  cd ${srcdir}/${_realname}-${pkgver}
  MSYS2_ARG_CONV_EXCL=PREFIX make -f Makefile.mingw PREFIX=${MINGW_PREFIX}
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}
  MSYS2_ARG_CONV_EXCL=PREFIX make -f Makefile.mingw install DESTDIR=${pkgdir} PREFIX=${MINGW_PREFIX}
}
